MAIN		ADR		R3, TEMP
		LDR		R0, [R3]
		LDR		R1, [R3, #4]!
		MOV		R11, #0
		BL		FDIV
		END
		
TEMP		DCD		0x13572AB, 0x23456EF, 0x3FF0F0F1, 0x4034B4B5
NINF		DCD		0xFF800000
PINF		DCD		0x7F800000
NAN		DCD		0xFFC00000
		
FDIV		STMED	R13!, {R4-R12,R14}
		BL		INIT
		TST		R11, #0x2
		LDMEDNE	R13!, {R4-R12,PC}
		TST		R11, #0x8
		MOVNE	R0, R1
		LDMEDNE	R13!, {R4-R12,PC}
		TST		R11, #0x1
		TSTNE	R11, #0x4
		ORRNE	R0, R0, #0xC00000
		LDMEDNE	R13!, {R4-R12,PC}
		TST		R11, #0x1
		ANDNE	R4, R1, #0x80000000
		EORNE	R0, R0, R4
		LDMEDNE	R13!, {R4-R12,PC}
		TST		R11, #0x4
		BEQ		NEXT3
		AND		R4, R0, #0x80000000
		EORS		R4, R4, R1
		MOVPL	R0, #0
		MOVMI	R0, #0x80000000
		LDMED	R13!, {R4-R12,PC}
NEXT3	CMP		R5, #0
		CMPEQ	R7, #0
		LDREQ	R0, [R3, #20]
		LDMEDEQ	R13!, {R4-R12,PC}
		CMP		R7, #0
		BNE		NEXT4
		AND		R4, R1, #0x80000000
		EORS		R4, R4, R0
		LDRMI	R0, [R3, #12]
		LDRPL	R0, [R3, #16]
		LDMED	R13!, {R4-R12,PC}
NEXT4	SUB		R6, R6, #127
		SUBS		R4, R4, R6
		CMP		R7, #0x800000
		SUBSNE	R4, R4, #1
		MOV		R6, #126
		STMED	R13!, {R1}
		CMP		R0, #0
		MOVMI	R8, #1
		MOVPL	R8, #0
		ADD		R14, PC, #4
		STMED	R13!, {R14}
		BL		RESULT2
		STMED	R13!, {R0}
		MOV		R4, R6
		MOV		R5, R7
		CMP		R1, #0
		MOV		R8, #0
		ADD		R14, PC, #4
		STMED	R13!, {R14}
		BL		RESULT2
		STMED	R13!, {R0}
		LDR		R1, [R3, #4]!
		STMED	R13!, {R3}
		BL		FMUL
		LDMED	R13!, {R3}
		MOV		R1, R0
		LDR		R0, [R3, #4]!
		BL		FSUB
		MOV		R10, #3
LOOP2	LDMED	R13, {R1}
		STMED	R13!, {R0}
		BL		FMUL
		MOV		R1, R0
		MOV		R0, #0x40000000
		BL		FSUB
		LDMED	R13!, {R1}
		BL		FMUL
		SUBS		R10, R10, #1
		BNE		LOOP2
		ADD		R13, R13, #4
		LDMED	R13!, {R1}
		BL		FMUL
		LDMED	R13!, {R1}
		CMP		R1, #0
		EORMI	R0, R0, #0x80000000
		LDMED	R13!, {R4-R12,PC}
		
FMUL		STMED	R13!, {R4-R12,R14}
		MOV		R4, #0
		MOV		R5, #0
		MOV		R8, #0
		BL		INIT
		TST		R11, #0x2
		LDMEDNE	R13!, {R4-R12,PC}
		TST		R11, #0x8
		MOVNE	R0, R1
		LDMEDNE	R13!, {R4-R12,PC}
		TST		R11, #0x1
		BEQ		NEXT1
		CMPNE	R7, #0
		ORREQ	R0, R0, #0xC00000
		ANDEQ	R4, R1, #0x80000000
		EOREQ	R0, R0, R4
		LDMEDEQ	R13!, {R4-R12,PC}
NEXT1	TST		R11, #0x4
		BEQ		NEXT2
		CMPNE	R5, #0
		ANDEQ	R4, R0, #0x80000000
		MOVEQ	R0, R1
		ORREQ	R0, R0, #0xC00000
		EOREQ	R0, R0, R4
		LDMEDEQ	R13!, {R4-R12,PC}
NEXT2	TST		R11, #0x1
		ANDNE	R4, R1, #0x80000000
		EORNE	R0, R0, R4
		LDMEDNE	R13!, {R4-R12,PC}
		TST		R11, #0x4
		ANDNE	R4, R0, #0x80000000
		MOVNE	R0, R1
		EORNE	R0, R0, R4
		LDMEDNE	R13!, {R4-R12,PC}
		STMED	R13!, {R10}
		MOV		R10, R0
		MOV		R9, R1
		STMED	R13!, {R10,R9}
		
MUL		ADDS		R4, R4, R6
		SUB		R4, R4, #127
		MOV		R0, R5, LSL #16
		MOV		R0, R0, LSR #16
		MOV		R1, R7, LSL #16
		MOV		R1, R1, LSR #16
		MOV		R2, #0
		BL		MUL32
		MOV		R6, R0
		MOV		R2, #0
		MOV		R0, R5, LSR #16
		BL		MUL24
		ADDS		R6, R6, R0, LSL #16
		ADC		R8, R8, R0, LSR #16
		MOV		R2, #0
		MOV		R0, R7, LSR #16
		MOV		R1, R5, LSL #16
		MOV		R1, R1, LSR #16
		BL		MUL24
		ADDS		R6, R6, R0, LSL #16
		ADC		R8, R8, R0, LSR #16
		MOV		R2, #0
		MOV		R0, R5, LSR #16
		MOV		R1, R7, LSR #16
		BL		MUL24
		ADD		R8, R8, R0
		BL		MULTNORM
		LDMED	R13!, {R4-R12,PC}
		
MULTNORM	MOVS		R7, R8, LSL #16
		ADDMI	R4, R4, #1
		
SHIFTER	MOVMI	R5, R6, LSR #24
		MOVPL	R5, R6, LSR #23
		MOVPL	R9, R6, LSL #9
		MOVMI	R9, R6, LSL #8
		MOVPL	R8, R8, LSL #18
		MOVMI	R8, R8, LSL #17
		MOV		R8, R8, LSR #9
		ORR		R5, R5, R8
		
NORMAL	CMP		R9, #0x80000000
		ADDHI	R5, R5, #1
		TEQ		R9, #0x80000000
		MOVEQ	R9, R5, LSL #31
		ADDEQ	R5, R5, R9, LSR #31
		CMP		R5, #0x800000
		ADDEQ	R4, R4, #1
		LDMED	R13!, {R0,R1}
		TEQ		R0, R1
		MOVMI	R8, #1
		LDMED	R13!, {R10}
		STMED	R13!, {R14}
		B		RESULT2
		
FSUB		STMED	R13!, {R4-R12,R14}
		EOR		R1, R1, #0x80000000
		BL		FADD
		LDMED	R13!, {R4-R12,PC}
		
FADD		STMED	R13!, {R4-R12,R14}
		BL		INIT
		BL		CHNEG
		BL		ADDH
		LDMED	R13!, {R4-R12,PC}
		
ADDH		STMED	R13!, {R14}
		CMP		R0, #0x80000000
		MOVEQ	R0, R1
		LDMEDEQ	R13!, {PC}
		TST		R11, #0x2
		LDMEDNE	R13!, {PC}
		TST		R11, #0x8
		MOVNE	R0, R1
		LDMEDNE	R13!, {PC}
		TST		R11, #0x1
		TSTNE	R11, #0x4
		TEQNE	R0, R1
		ORRMI	R0, R0, #0xC00000
		LDMEDMI	R13!, {PC}
		TST		R11, #0x1
		LDMEDNE	R13!, {PC}
		TST		R11, #0x4
		MOVNE	R0, R1
		LDMEDNE	R13!, {PC}
		CMP		R4, R6
		CMNEQ	R5, R7
		BNE		NEXT
		EORSEQ	R8, R0, R1
		MOVMI	R0, #0
		LDMEDMI	R13!, {PC}
NEXT		SUBS		R8, R4, R6
		BMI		LESSER
		CMP		R8, #23
		LDMEDGT	R13!, {PC}
		RSB		R9, R8, #32
		MOV		R9, R7, LSL R9
		MOV		R7, R7, ASR R8
		ADD		R5, R5, R7
		
RESULT1	CMP		R5, #0
		MOVEQ	R4, #0
		MOV		R8, #0
		MVNMI	R5, R5
		ADDMI	R5, R5, #1
		MOVMI	R8, #1
		CMP		R5, #0x800000
		MOVGT	R7, #9
		BGT		GREATER
		MOVNE	R7, #0
		BLNE		NORMALISE
		CMP		R8, #1
		RSB		R10, R7, #32
		MOV		R10, R9, LSR R10
		MOV		R9, R9, LSL R7
		ADDNE	R5, R5 ,R10
		SUBEQ	R5, R5, R10
		BEQ		NEGS
		CMP		R9, #0x80000000
		ADDHI	R5, R5, #1
		TEQ		R9, #0x80000000
		MOVEQ	R9, R5, LSL #31
		ADDEQ	R5, R5, R9, LSR #31
		MOV		R5, R5, LSL #9
		MOV		R5, R5, LSR #9
		
RESULT2	MOV		R9, #0
		CMP		R4, #255
		MOVGE	R4, #255
		MOVGE	R5, #0
		LSL		R5, R5, #9
		LSR		R5, R5, #9
		CMP		R4, #0
		CMPEQ	R5, #0
		ORREQ	R0, R5, R8, LSL #31
		LDMEDEQ	R13!, {PC}
		CMP		R4, #0
		RSBLE	R4, R4, #1
		ORRLE	R5, R5, #0x800000
		RSBLE	R9, R4, #32
		MOVLE	R9, R5, LSL R9
		MOVLE	R5, R5, LSR R4
		MOVLE	R4, #0
		CMP		R9, #0x80000000
		ADDHI	R5, R5, #1
		TEQ		R9, #0x80000000
		MOVEQ	R9, R5, LSL #31
		ADDEQ	R5, R5, R9, LSR #31
		ORR		R5, R5, R8, LSL #31
		MOV		R4, R4, LSL #23
		ORR		R5, R5, R4
		MOV		R0, R5
		LDMED	R13!, {PC}
		
GREATER	MOVS		R9, R9, LSL #1
		ADCS		R5, R5, R5
		SUB		R7, R7, #1
		BCC		GREATER
		MOV		R9, R5, LSL #23
		MOV		R5, R5, LSR #9
		ADD		R4, R4, R7
		CMP		R8, #1
		BEQ		NEGS
		CMP		R9, #0x80000000
		ADDHI	R5, R5, #1
		TEQ		R9, #0x80000000
		MOVEQ	R9, R5, LSL #31
		ADDEQ	R5, R5, R9, LSR #31
		B		RESULT2
		
NEGS		CMP		R9, #0x80000000
		SUBHI	R5, R5, #1
		TEQ		R9, #0x80000000
		MOVEQ	R9, R5, LSL #31
		SUBEQ	R5, R5, R9, LSR #31
		B		RESULT2
		
LESSER	SUBS		R8, R6, R4
		CMP		R8, #23
		MOVGE	R0, R1
		LDMEDGE	R13!, {PC}
		RSB		R9, R8, #32
		MOV		R9, R5, LSL R9
		MOV		R5, R5, ASR R8
		ADD		R7, R7, R5
		MOV		R4, R6
		MOV		R5, R7
		B		RESULT1
		
INIT		STMED	R13!, {R14}
		ADD		R4, R0, R0
		MOV		R4, R4, LSR #24
		MOV		R5, R0, LSL #9
		MOV		R5, R5, LSR #9
		ADD		R6, R1, R1
		MOV		R6, R6, LSR #24
		MOV		R7, R1, LSL #9
		MOV		R7, R7, LSR #9
		STMED	R13!, {R7}
		CMP		R4, #0
		ORRNE	R5, R5, #0X800000
		MOV		R7, #-1
		BLEQ		NORMALISE
CHR0		CMP		R4, #255
		BNE		PROG1
		CMPEQ	R5, #0x800000
		ADDEQ	R11, R11, #1
		ADDNE	R11, R11, #2
PROG1	LDMED	R13!, {R7}
		STMED	R13!, {R4,R5}
		MOV		R4, R6
		MOV		R5, R7
		CMP		R4, #0
		ORRNE	R5, R5, #0X800000
		MOV		R7, #-1
		BLEQ		NORMALISE
CHR1		CMP		R4, #255
		BNE		PROG2
		CMPEQ	R5, #0x800000
		ADDEQ	R11, R11, #4
		ADDNE	R11, R11, #8
PROG2	MOV		R6, R4
		MOV		R7, R5
		LDMED	R13!, {R4,R5,PC}
		
CHNEG	CMP		R0, #0
		MVNMI	R5, R5
		ADDMI	R5, R5, #1
		CMP		R1, #0
		MVNMI	R7, R7
		ADDMI	R7, R7, #1
		MOV		PC, R14
		
NORMALISE	CMP		R5, #0
		MOVEQ	PC, R14
		MOV		R5, R5, LSL #8
LOOP		MOVS		R5, R5, LSL #1
		ADD		R7, R7, #1
		BPL		LOOP
		SUB		R4, R4, R7
		MOV		R5, R5, LSR #8
		MOV		PC, R14
		
MUL32	LSLS		R3, R0, #17
		ADDCS	R2, R2, R1, LSL #15
		ADDMI	R2, R2, R1, LSL #14
		LSLS		R3, R0, #19
		ADDCS	R2, R2, R1, LSL #13
		ADDMI	R2, R2, R1, LSL #12
		LSLS		R3, R0, #21
		ADDCS	R2, R2, R1, LSL #11
		ADDMI	R2, R2, R1, LSL #10
		LSLS		R3, R0, #23
		ADDCS	R2, R2, R1, LSL #9
		ADDMI	R2, R2, R1, LSL #8
MUL24	LSLS		R3, R0, #25
		ADDCS	R2, R2, R1, LSL #7
		ADDMI	R2, R2, R1, LSL #6
		LSLS		R3, R0, #27
		ADDCS	R2, R2, R1, LSL #5
		ADDMI	R2, R2, R1, LSL #4
		LSLS		R3, R0, #29
		ADDCS	R2, R2, R1, LSL #3
		ADDMI	R2, R2, R1, LSL #2
		MOVS		R3, R0, LSL #31
		ADDCS	R2, R2, R1, LSL #1
		ADDMI	R2, R2, R1
		MOV		R0, R2
		MOV		PC, R14
